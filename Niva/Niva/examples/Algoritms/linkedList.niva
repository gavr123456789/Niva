type Node v: Int next: Node?
constructor Node v: Int = Node v: v next: null

Node str -> String =
    next unpack: [ "$v, " + it str] or: "$v"

nodeInt =
Node
    v: 545
    next: (
        Node
            v: 123
            next: (Node v: 345)
    )

nodeInt str echo



type LinkedList head: Node? tail: Node? size: Int

constructor LinkedList empty =
    LinkedList head: null tail: null size: 0

LinkedList isEmpty = size == 0
LinkedList str = this isEmpty => "Empty!" |=> head unpackOrPANIC str
mut LinkedList push(v): Int -> LinkedList = [
    head <- Node v: v next: head
    tail == null => tail <- head
    size <- size inc
    ^this
]

mut LinkedList add(v): Int -> mut LinkedList =
  this isEmpty => [
    this push: v
    this
  ] |=> [
    tail unpack: [
      it next: (Node v: v)
      tail <- it next
      size <- size inc
    ]
    this
  ]


LinkedList at(i): Int -> Node? = [
  mut curr = head
  0..<i forEach: [
    curr unpack: [curr <- it next]
  ]
  ^curr
]

LinkedList forEach: [Int -> Unit] =
  0..<(size - 1) forEach: [
    forEach it: (this at: it, unpackOrPANIC v)
  ]


LinkedList map: [Int -> Int] -> LinkedList = [
   newLinkedList: mut LinkedList = LinkedList empty
   this forEach: [
     newLinkedList add: (map it: it)
   ]
  ^newLinkedList
]


list: mut LinkedList = LinkedList empty
list push: 1, push: 2, push: 3
list add: -1, add: -2, add: -3

list forEach: [it echo]

"element 2" echo
list at: 2, unpackOrPANIC v echo
"full list" echo
list str echo
list map: [it + 9], str echo
