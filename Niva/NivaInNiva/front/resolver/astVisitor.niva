union ResolveErrors =
| ErrorNoSuchType tok: Token


union AstVisitor =
| PrintingVisitor //sb: StringBuilder
| TypeDeclVisitor db: TyperDB 
| MsgDeclVisitor
| GoBackendVisitor

AstVisitor run: statements::List::Statement= [
  statements forEach: [
    this visit: it
  ]
]

PrintingVisitor visit: st::Statement = [
  st echo
  "--------------" echo
]

/// returns pkg to message decl
TypeDeclVisitor visitAllTypesThenTypesAndMethods: fileToStatements::Map(String, List::Statement) -> List(Tuple(String, MessageDecl)) = [
  pkgTotypeDecls2::mut List(Tuple(String, TypeDecl)) = {}!
  // msgDecls::mut Map(String, MessageDecl) = #{}!
  noFieldsTypes::mut Map(String, TypeT) = #{}!
  msgDecls2::mut List(Tuple(String, MessageDecl)) = {}!


  // first iteration, fill the lists of statements, create pkgs
  fileToStatements forEach: [file, statements -> 
    pkgName = file

    db addNewPkgIfNotExists: pkgName

    // TODO unions, enums, aliases
    // save all types (without fields), and msg decls
    statements forEach: [
      | it
      | TypeDecl => [
        noFieldsType = TypeT noFieldsWithName: it name pkgName: pkgName
        noFieldsTypes at: it name put: noFieldsType
        pkgTotypeDecls2 add: (Tuple x: pkgName y: it)
      ]
      | MessageDecl => [
        msgDecls2 add: (Tuple x: pkgName y: it) 
        Unit]
      // msgDecls at: pkgName put: it debug
      | UnionDecl => TO DO: "UnionDecl"
      | EnumDecl => TO DO: "EnumDecl"
      |=> []
    ]
  ]

  fillTypesAndMessages = [
    pkgTotypeDecls2 forEach: [ tuple ->
      pkgName = tuple x
      typeDecl = tuple y

      readyToAddToDBType = .fillFields: typeDecl knownTypes: noFieldsTypes
      readyToAddToDBType unpack: [ resolvedType ->

        db 
          addNewType: resolvedType 
          inPkg: pkgName

        
        db 
          addNewConstructor: resolvedType generateDefaultConstructor 
          forType: resolvedType 

        "added type " + resolvedType name + " in pkg " + pkgName, log
      ]
      readyToAddToDBType == null ifTrue: [
        name = typeDecl name
        TO DO: "cant fill fields for $name"
      ]
    ]
    

    // msg decls resolving
    // here we should have all the types resolved
    msgDecls2 forEach: [ tuple ->
      pkg = tuple x 
      msg = tuple y

      // get real type for receiver
      receiverType = db findByTypeName: msg receiver name inPkg: null // we cant know the concrete pkg here
      // get real type for each arg
      argsT = msg args map: [ arg ->
        argAstType = arg astType name
        argType = db findByTypeName: argAstType inPkg: null // we cant know the concrete pkg here
        TypeTField name: arg name typeT: argType
      ]


      getReturnType = [
        rt = msg returnType 
        | rt 
        | null => db findByTypeName: "Unit" inPkg: null 
        |=> db findByTypeName: rt name inPkg: null 
      ]

      
      astOfReceiver = msg receiver 
      isMutable = | astOfReceiver
      | AstType => astOfReceiver isMut
      | AstLambdaType => false
      
      method = NivaMethod 
        name: msg name 
        args: argsT 
        returnType: (msg getReturnTypeFromDb: db) 
        forMut: isMutable 
        possibleErrors: {}
        kind: MessageSendKind.Call

      db addNewMethod: method forType: receiverType inProtocol: null
      "added method " + method name, log
    ]
  
  ] do

  ^ msgDecls2
]

/// if no return type and has body then its Unit
/// if no return type and isSingleExpr then its unknown
MessageDecl getReturnTypeFromDb: db::TyperDB -> NivaType = [
  rt = returnType
 ^| rt
  | null => [
    db findByTypeName: "Unit" inPkg: null 
  ]
  |=> db findByTypeName: rt name inPkg: null 
]

/// returns a type with fields filled with real types, ready to add to db
/// returns null if such type was not found in knownTypeNames or db
/// and adds that as error to listOfErrors
TypeDeclVisitor fillFields: st::TypeDecl knownTypes::mut Map(String, TypeT) -> TypeT? = [

  typeStub = knownTypes at: st name

  | typeStub
  | null => [
    db listOfErrors add: (ErrorNoSuchType tok: st token)
    ^ null
  ]
  |=> [


    fields = st fields forEach: [
      fieldName = it astType name
      knownNames = knownTypes keys
      // search in known types, if not found search in db, db always has core types, 
      // known types contains every type from AST decls
      maybeFieldType = knownTypes at: fieldName
      fieldType = | maybeFieldType
      | null => db findByTypeName: fieldName inPkg: null
      |=> maybeFieldType


      field = TypeTField name: it name typeT: fieldType
      typeStub fields add: field

      getter = NivaMethod 
        name: it name 
        args: {} 
        returnType: fieldType 
        forMut: false // false because its getter
        possibleErrors: {}
        kind: MessageSendKind.Getter
      
      db addNewMethod: getter forType: typeStub inProtocol: "getters"
    ]
    
    ^ typeStub
  ]
]

MsgDeclVisitor processBodyes: pkg2Msg::List(Tuple(String, MessageDecl)) db::TyperDB = [
  pkg2Msg forEach: [tuple -> 
    pkgName = tuple x 
    msgDecl = tuple y 
    msgDecl resolveBodyWithDb: db
  ]
]

// every message with its context attached
// context is a tree like structure with parents
// every context contains all the symbols inside, resolved
type MessagesWithContext
  data: Map(MessageDecl, Context)


MessageDecl resolveBodyWithDb: db::TyperDB = [
  // add receiver  as "this" to context
  createContextWithThisAndArgs = [
    c = Context root
    receiver = db findByTypeName: receiver name inPkg: null
    receiver name == "Nothing" ifTrue: [
      TO DO: "You cant declare methods for Nothing type"
    ]
    
    c defineVar: "this" withType: receiver


    args forEach: [
      argType = db findByTypeName: it astType name inPkg: null
      c defineVar: it name withType: argType
    ]

    c
  ]

  c = createContextWithThisAndArgs do
  body resolve: db withContext: c
]




AstVisitor visit: st::Statement = [
  | this
  | PrintingVisitor => this visit: st
  | TypeDeclVisitor => [
    | st
    | TypeDecl => this visit: st
    |=>[]
    Unit
  ]
  | MsgDeclVisitor => [
    TO DO
  ]
  | GoBackendVisitor => [
    this visit: st
  ]
]

// todo go deep to call visitor on each child note
// Statement accept: visitor::AstVisitor = 
// | this
// | Expr => [
//   visitor visit: this
//   this exprAccept: visitor
// ]
// | Declaration => this declAccept: visitor
// | Assign => [
//   visitor visit: this
//   this value exprAccept: visitor
// ]
// | DestructingAssign => [
//   visitor visit: this
//   this value exprAccept: visitor
// ]


// Expr exprAccept: visitor::AstVisitor -> Unit = 
// | this
// | LiteralExpr | Identifier | ReturnStatement => [visitor visit: this]
// | ExprInBrackets => [
//   this value exprAccept: visitor
// ]
// | TypedExpr => []
// | CollectionLiteral => [
//   this elements forEach: [it exprAccept: visitor]
// ]
// | MessageSend => [
//   | this
//   | UnaryMsg => [
//     visitor visit: this
//   ]
//   | BinaryMsg => [
//     this args forEach: [it expr exprAccept: visitor]
//     visitor visit: this
//   ]
//   | KeywordMsg => [
//     this args forEach: [it expr exprAccept: visitor]
//     visitor visit: this
//   ]

// ]
// | CodeBlock => [
//   TO DO
// ]
// | Match => TO DO
// | VarDeclaration => TO DO



// Declaration declAccept: visitor::AstVisitor -> Unit = 
// | this
// | TypeDecl =>  visitor visit: this

// | EnumDecl => visitor visit: this
// | UnionDecl => visitor visit: this
// | MessageDecl => [
//   // Cant visit receiver type and return types since they are not declarations
//   visitor visit: this
//   this body forEach: [it accept: visitor]
// ]
