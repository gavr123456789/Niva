
/// abcd
/// __^
/// pos = 0, because it increases only when tok matched
/// readPos = 3, because it one step ahead
/// ch = 'c'
type Lexer
  input: String
  pos: Int // updates when new token read
  readPos: Int // updates every step
  ch: Char
  line: Int
  posOnLine: Int

constructor Lexer input::String = [
  l::mut Lexer = Lexer input: input pos: 0 readPos: 0 ch: '\u0000' line: 1 posOnLine: 0
  l step 
  ^l 
] 

extend mut Lexer [
  on printState = [
    "---" debug
    input debug
    readPos debug
    ch debug
    "---" debug
  ]
  on step = [
    // readPos always bigger than current read char pos
    readPos >= input count 
      ifTrue: [
        ch <- '\u0000'
      ] 
      ifFalse: [ch <- input at: readPos] 
      
    inputCount = input count
    readPos >= 75 => Error throwWithMessage: "readPos >= 75, $readPos, $inputCount", orPANIC
    // pos <- readPos
    readPos <- readPos inc
    posOnLine <- posOnLine inc
  ]

  on peekChar = readPos == input count 
    ifTrue:  ['\u0000']
    ifFalse: [input at: (.readPos)]
  
  on match: char::Char -> Boolean = 
    .ch == char 
      ifTrue: [
        .step
        true
      ] ifFalse: [
        false
      ]
  
  on stepMatch: char::Char -> Boolean = [
    // input count debug
    .step 
    ^.match: char
  ]

  on createToken: kind::TokenType lexeme::String -> Token = [

    tok = Token
      kind: kind
      lexeme: lexeme
      line: line
      start: posOnLine - lexeme count end: posOnLine//lexeme count
    
    // tok debug
    pos <- readPos
    ^ tok
  ]

  on nextToken -> Token! = [
    .skipWhiteSpaces
    l = ch toString

    tok = | ch 
    | '\u0000' => [
      ^.createToken: TokenType.EOF lexeme: l]
    | '=' => [
      .step
      | ch 
      | '=' => .createToken: TokenType.BinarySymbol lexeme: "=="
      | '>' => .createToken: TokenType.BinarySymbol lexeme: "=>"
      |=> .createToken: TokenType.Assign lexeme: "="
    ]

    | '"' => [
      result = .parseStringLike: '"'
      ^ .createToken: TokenType.String lexeme: result
    ]
    | '/' => .stepMatch: '/', 
      ifTrue:  [
        .skipCurrentLine
        .createToken: TokenType.Comment lexeme: "//"
      ] 
      ifFalse: [.createToken: TokenType.BinarySymbol lexeme: "/"]

    | '!' => .stepMatch: '=', 
      ifTrue:  [.createToken: TokenType.BinarySymbol lexeme: "!="] 
      ifFalse: [.createToken: TokenType.Bang lexeme: "!"]

    | '^' => .createToken: TokenType.Return   lexeme: l

    // parens
    | '(' => .createToken: TokenType.OpenParen   lexeme: l
    | ')' => .createToken: TokenType.CloseParen  lexeme: l
    | '{' => .createToken: TokenType.OpenBrace   lexeme: l
    | '}' => .createToken: TokenType.CloseBrace  lexeme: l
    | '#' => [
      .step
      | ch 
      | '{' => .createToken: TokenType.OpenBraceHash lexeme: "#{"
      | '(' => .createToken: TokenType.OpenParenHash lexeme: "#("
      |=> Error throwWithMessage: "only { or ( can be after #"
    ]

    // binary symbols
    |'<' => .stepMatch: '=', 
      ifTrue:  [.createToken: TokenType.BinarySymbol lexeme: "<="] 
      ifFalse: [.createToken: TokenType.Bang lexeme: "<"]

    |'>' => .stepMatch: '=', 
      ifTrue:  [.createToken: TokenType.BinarySymbol lexeme: ">="] 
      ifFalse: [.createToken: TokenType.Bang lexeme: ">"]
    |'-' => .stepMatch: '>', 
      ifTrue:  [.createToken: TokenType.ReturnArrow lexeme: "->"] 
      ifFalse: [.createToken: TokenType.BinarySymbol lexeme: "-"]
    
    |'+'|'*' => .createToken: TokenType.BinarySymbol lexeme: l
    // other staff
    | ',' =>      .createToken: TokenType.Comma lexeme: l
    
    |=> [
      // identifier
      ch isLetter || (ch == '_') ifTrue: [
        lit = this readIdentifier
        tt = Token lookupKeyword: lit

        // colon after identifier
        ^.match: ':', // early return because we dont need another step
          ifTrue:  [
            .stepMatch: ':',
              ifTrue: [.createToken: TokenType.IdentifierDoubleColon lexeme: lit + "::"]
              ifFalse: [.createToken: TokenType.IdentifierColon lexeme: lit + ":"]
          ] 
          ifFalse: [.createToken: tt lexeme: lit ]
      ]
      // digit
      ch isDigit ifTrue: [
        lit = .parseNumber
        ^ .createToken: TokenType.Integer lexeme: lit
      ]
      
      // fail
      .createToken: TokenType.Invalid lexeme: l
    ]

    .step
    ^ tok
  ]

  on lex -> MutableList::Token! = [
    mut newTok = .nextToken
    mut tokens::MutableList::Token = {newTok}m
    [newTok kind != TokenType.EOF] whileTrue: [
      newTok <- .nextToken
      tokens add: newTok
    ]
    ^ tokens
  ]
  /// skips any symbol until "\n"
  on skipCurrentLine = [
    [(ch != '\n') && (ch != '\u0000')] whileTrue: [
      // "'$ch'" echo
      .step
    ]
    .match: '\n'
  ]
]


enum StrMode = Single | Multi

/// assume that delimiter was already eaten
mut Lexer parseStringLike: delimiter::Char -> String = [
  mut mode = StrMode.Single

  .match: delimiter // eat the first "
  localPos = pos
  [ch != delimiter] whileTrue: [
    ch == '\n' ifTrue: [mode <- StrMode.Multi]
    .step
  ]
  .match: delimiter // eat the last "
  
  result = input slice: localPos..<readPos dec
  ^result
]

mut Lexer readIdentifier -> String = [
  localPos = pos
  [ch isLetter] whileTrue: [
    .step
  ]

  result = input slice: localPos..<readPos dec
  ^result
]

mut Lexer parseNumber -> String = [
  localPos = pos 
  [ch isDigit] whileTrue: [
    .step
  ]

  result = input slice: localPos..<readPos dec
  ^result
]

mut Lexer skipWhiteSpaces = [

  [(ch == ' ') || (ch == '\t') || (ch == '\n') || (ch == '\r')] whileTrue: [
    ch == '\n' ifTrue: [
      line <- line inc
      posOnLine <- 0

    ]
    .step
  ]
 
]
