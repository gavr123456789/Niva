extend mut Parser [

  /// inc inc inc
  on parseManyIdentSeparated(sep): TokenType? -> mut List(Identifier) = [
    result::mut List(Identifier) = {}!

    mut c = true
    [c] whileTrue: [
      tok = .peek

      | tok kind
      | TokenType.Identifier => [
        // stop if new unary on new line
        result isNotEmpty && (tok line > result last token line), ifTrue: [
          c <- false
        ] ifFalse: [
          identifier = .step
          expr = (.parseDotSeparatedIdentifier: tok)
          result add: expr
          sep unpack: [.match: it]
          Unit
        ]
      ]
      |=> [
        c <- false
      ]
    ]
    // "parseManyIdent $result" echo
    ^result
  ]

  /// + 2 inc inc + 3 dec dec
  on parseManyBinary -> List::NameAndExpr = [
      result::mut List::NameAndExpr = {}!
      mut c = true
      [c] whileTrue: [
        tok = .peek
        // tok lexeme echo
        // this is double check for binary, we already did it on the call side
        // ... but we still need it for "recursion" to get the next +
        | tok kind
        | TokenType.BinarySymbol => [
          binarySymbol = .step
          // binarySymbol kind echo
          // + ^1 inc inc + 2
          receiver = (.primary: .step) unpackOrMsg: "Cant find primary" // TODO тут может быть не праймари, а унари, или в скобках что угодно поэтому парсить нужно не праймари, не, надо просто добавить в парсинг праймари скобочных выражений
          // + 1^ inc inc + 2
          unaryList = .peek line == receiver token line, ifTrue: [
            .parseManyIdentSeparated: null
          ] ifFalse: [{}!]


          unaryList isEmpty, ifTrue: [
            // + 1 + 2
            binSymbol = Identifier fromToken: tok
            result add: (NameAndExpr name: (binSymbol) expr: receiver)
          ] ifFalse: [
            args = unaryList map: [
              NameAndExpr name: it expr: it
            ]
            mut tempUnary = UnaryMsg
              selector: unaryList first
              kind: MessageSendKind.Call
              receiver: receiver
              token: tok

            unaryList drop: 1, forEach: [
              newUnary = UnaryMsg
                selector: it
                kind: MessageSendKind.Call
                receiver: tempUnary
                token: tok
              tempUnary <- newUnary
            ]

            unary = tempUnary

            // + 1 inc inc + 2
            binSymbol = Identifier fromToken: tok
            result add: (NameAndExpr name: (binSymbol) expr: unary)
          ]

          Unit
        ]
        |=> [
          c <- false
          Unit
        ]
      ]
      ^ result toList
    ]
]
