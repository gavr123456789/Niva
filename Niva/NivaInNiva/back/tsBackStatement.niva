
Statement toTSFromPkg: currentPkg::String db::TyperDB -> String=
    | this
    | TypeDecl => [
        // class Person {
        //     constructor(name, age) {
        //         this.name = name;
        //         this.age = age;
        //     }
        // }
        sb = StringBuilder new

        sb append: "export class "
        sb append: this name
        sb append: " {\n"
        sb append: "    constructor("

        argsStr = this fields joinWith: "," transform: [it name]

        sb append: argsStr
        sb append: ") {\n"

        // foreach arg
        this fields forEach: [
            name = it name
            sb append: "        this.$name = $name;\n"
        ]


        sb append: "    }\n"
        sb append: "}"

        // and probably function CLASS_new_C()
        sb toString
    ]
    | EnumDecl => [TO DO]
    | UnionDecl => [TO DO]
    | MessageDecl => [

        //export function main_Person__withAge(_receiver, withAge) {
        //  //destruct this age, name
        //  let age = _receiver.age
        //  let name = _receiver.name
        //  age = withAge
        //}

        sb = StringBuilder new

        // resolve receiver type to get mangled name
        receiverType = db findByTypeName: receiver name inPkg: null
        receiverFields =
            | receiverType
            | Complex => [receiverType fields]
            |=> {TypeTField}
        mangledReceiverName = receiverType toMangledName

        suffix = name substringAfter: "__"
        hasSuffix = suffix != name
        baseName = hasSuffix ifTrue: [name substringBefore: "__"] ifFalse: [name]

        // if keyword (has colons), reconstruct baseName from args
        finalBaseName = (baseName contains: ":")
            ifTrue: [args joinWith: "_" transform: [it name]]
            ifFalse: [baseName]

        methodName = mangledReceiverName + "_" + finalBaseName + (hasSuffix ifTrue: ["__" + suffix] ifFalse: [""])

        sb append: "export function "
        sb append: methodName


        argsStr = "_this" +
            (args isNotEmpty ifTrue: [", "]
                             ifFalse: [""]) +
            (args joinWith: ", "
                  transform: [it name])


        sb append: "("
        sb append: argsStr
        sb append: ") {\n"

        // add receiver destructing
        //  let age = _this.age
        //  let name = _this.name
        generateDestruct = [
            // let fieldName = _this.fieldName
            > receiverFields
            receiverFields forEach: [
               > it name
               sb append: "    "
               sb append: "let "
               sb append: it name
               sb append: " = _this."
               sb append: it name
               sb append: "\n"
            ]
        ] do

        this body typedBody2 forEach: [expr ->
            sb append: "    "
            sb appendLine: (expr toTSFromPkg: currentPkg)
        ]

        sb append: "}"
        sb toString
    ]
    |=> [
        "toTS: skipping top level expr: " + this token toString, log
        ""
    ]
